# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pangomm
pkgbase=mingw-w64-${_realname}-next
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-next"
pkgver=2.43.1
pkgrel=1
pkgdesc="C++ bindings for pango - next 2.44 (mingw-w64)"
arch=('any')
url="https://gtkmm.sourceforge.io/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-cairomm-next" "${MINGW_PACKAGE_PREFIX}-glibmm-next" "${MINGW_PACKAGE_PREFIX}-pango")
options=(staticlibs !strip)
source=("${_realname}-${pkgver}.tar.gz")
sha256sums=('62b7fc388dbb5c74985a18700c1f44d117ec28337de44acd83053a69248c41f9')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -i -e "s,AC_DISABLE_STATIC,AC_DISABLE_STATIC\nlt_cv_deplibs_check_method='pass_all'," configure.ac
  autoconf --force
}

build() {
  CPPFLAGS+=" -D_REENTRANT"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-documentation

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-next/COPYING"
}
